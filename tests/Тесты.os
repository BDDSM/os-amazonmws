#Использовать ".."
#Использовать asserts
#Использовать json

Перем ИдентификаторПродавца;
Перем КлючAWS;
Перем СекретныйКлюч;

Перем ИдентификаторОтчета;

Процедура ПередЗапускомТеста() Экспорт
	
	ИдентификаторПродавца = "";
	КлючAWS = "";
	СекретныйКлюч = "";
	
	Путь = ОбъединитьПути(ТекущийСценарий().Каталог, "fixtures", "keys.json");
	Файл = Новый Файл(Путь);
	Если Файл.Существует() Тогда
		
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.Прочитать(Файл.ПолноеИмя, КодировкаТекста.UTF8);
		
		Парсер = Новый ПарсерJSON();
		
		Результат = Парсер.ПрочитатьJSON(ТекстовыйДокумент.ПолучитьТекст());
		ИдентификаторПродавца = Результат.Получить("ИдентификаторПродавца");
		КлючAWS = Результат.Получить("КлючAWS");
		СекретныйКлюч = Результат.Получить("СекретныйКлюч");
	Иначе
		// получим все из параменных среды
		ИдентификаторПродавца = ПолучитьПеременнуюСреды("AMAZON_MWS_SELLER_ID");
		КлючAWS = ПолучитьПеременнуюСреды("AMAZON_MWS_AWS_KEY");
		СекретныйКлюч = ПолучитьПеременнуюСреды("AMAZON_MWS_SECRET_KEY");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	
КонецПроцедуры

&Тест
Процедура ПроверитьПолучениеСпискаТоваров() Экспорт
	
	Площадка = ПлощадкиAmazon.Испания;
	
	Клиент = Новый КлиентAmazonMWS(ИдентификаторПродавца, СекретныйКлюч, КлючAWS);
	КодОтчета = Клиент.ЗапроситьСписокТоваровПоПлощадке(Площадка, ТипыОтчетовAmazon.ТоварыВесьСписок);
	
	Ожидаем.Что(КодОтчета, "Код отчета получен").Заполнено();
	
	МодульОбщегоНазначения.СделатьПаузу(15);
	
	ИдентификаторОтчета = Клиент.ПолучитьИдентификаторОтчета(Площадка, КодОтчета);
	Ожидаем.Что(ИдентификаторОтчета, "Идентификатор отчета получен").Заполнено();
	
	СодержимоеОтчета = Клиент.ПолучитьСодержимоеОтчета(Площадка, ИдентификаторОтчета);
	Ожидаем.Что(ТипЗнч(СодержимоеОтчета) = Тип("Строка"), "Содержимое отчета в виде текста").Равно(Истина);
	
	ДанныеОтчета = МодульОбщегоНазначения.ТаблицаЗначенийТзТекста(СодержимоеОтчета);
	Ожидаем.Что(ТипЗнч(ДанныеОтчета) 
	= Тип("ТаблицаЗначений"), "Содержимое отчета обработано в таблицу значений").Равно(Истина);
	
КонецПроцедуры

&Тест
Процедура ПроверитьЗаполнениеНоменклатуры() Экспорт
	
	Площадка = ПлощадкиAmazon.Испания;	
	Клиент = Новый КлиентAmazonMWS(ИдентификаторПродавца, СекретныйКлюч, КлючAWS);
	СодержимоеОтчета = Клиент.ПолучитьСодержимоеОтчета(Площадка, ИдентификаторОтчета);
	ДанныеОтчета = МодульОбщегоНазначения.ТаблицаЗначенийТзТекста(СодержимоеОтчета);
	
	Ожидаем.Что(ДанныеОтчета.Количество(), "Номенклатуры получено больше 0").Больше(0);
	
	СтрокаОтчета = ДанныеОтчета[0];
	
	Номенклатура = Новый НоменклатураAmazon();
	Номенклатура.Заполнить(СтрокаОтчета);
	
	Ожидаем.Что(Номенклатура.Наименование, "Наименование номенклатуры заполнено").Заполнено();
	
КонецПроцедуры